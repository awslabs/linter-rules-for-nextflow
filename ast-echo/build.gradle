plugins {
    id 'java'
    id 'application'
}

group = 'software.amazon.groovy'
version = '0.1'

application {
    mainClass = 'software.amazon.groovy.AstEchoCli'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'org.codehaus.groovy:groovy-all:3.0.24'  // to get access to the GroovyAST and support
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'

    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes "Main-Class": application.mainClass
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}